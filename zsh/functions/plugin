#! /usr/bin/env zsh

_realpath() { for f in "$@"; do echo ${f}(:A); done }

_git() {
	if [[ ! -d $2 ]]; then
		mkdir -p $2
		git clone --recursive $1 $2
	else
		git -C $2 pull --recurse-submodules
	fi
}

zplugin() {
	local zcache=${ZSH_CACHE:-${XDG_CACHE_HOME:-~/.cache}/zsh}
	local zname="$1"; shift;
	local zpath="${zcache}/github/${zname}"

	print -- "\n\e[1;32m$zname\e[0m" >&2
	_git "https://github.com/$zname" "$zpath" >&2

	# Build zplugin section
	print
	print "# $zname"

	if [ $# -lt 1 ]; then
		# Prepend PATH if executables are found
		if find ${zpath} -perm +111 -type f -maxdepth 0; then
			print -r -- "PATH=$zpath"':$PATH'
		fi

		# Prepend fpath if functions found
		if [[ -f $zpath/functions ]]; then
			print -r -- "fpath=($zpath/functions"' $fpath)'
		fi

		# Determine entry point
		if [[ -f ${zpath}/${zpath:t}.zsh ]]; then
			print -r -- "source ${zpath}/${zpath:t}.zsh"
		elif [[ -f ${zpath}/${zpath:t}.plugin.zsh ]]; then
			print -r -- "source ${zpath}/${zpath:t}.plugin.zsh"
		elif [[ -f ${zpath}/${zpath:t}.zsh-theme ]]; then
			print -r -- "source ${zpath}/${zpath:t}.zsh-theme"
		fi
	fi

	while (($#)); do
		if [[ $1 == '--source' ]]; then
			print -r -- "source ${$(_realpath $zpath/$2)}"
			shift; shift
		elif [[ $1 == '--fpath' ]]; then
			print -r -- "fpath=(${$(_realpath $zpath/$2)}"' $fpath)'
			shift; shift
		elif [[ $1 == '--path' ]]; then
			print -r -- "PATH+=${$(_realpath $zpath/$2)}"':$fpath'
			shift; shift
		else
			echo "unknown option: $1"
			return 1
		fi
	done
}

zplugin "$@"
